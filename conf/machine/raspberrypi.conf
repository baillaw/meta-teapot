#@TYPE: Machine
#@NAME: RaspberryPi Development Board (Mainline)
#@DESCRIPTION: Machine configuration for the RaspberryPi http://www.raspberrypi.org/ Board

require conf/machine/include/arm/arch-armv6.inc

DEFAULTTUNE ?= "armv6"
TUNEVALID[arm1176jzfs] = "Enable arm1176jzfs specific processor optimizations"
TUNE_CCARGS += "${@bb.utils.contains("TUNE_FEATURES", "arm1176jzfs", "-mtune=arm1176jzf-s", "", d)}"
TUNE_CCARGS += "${@bb.utils.contains("TUNE_FEATURES", "vfp", "-mfpu=vfp", "", d)}"

AVAILTUNES += "arm1176jzfs arm1176jzfshf"
TUNE_FEATURES_tune-arm1176jzfs = "${TUNE_FEATURES_tune-armv6} arm1176jzfs"
TUNE_FEATURES_tune-arm1176jzfshf = "${TUNE_FEATURES_tune-arm1176jzfs} callconvention-hard"

ARMPKGARCH_tune-arm1176jzfs = "arm1176jzfs"
ARMPKGARCH_tune-arm1176jzfshf = "arm1176jzfs"

PACKAGE_EXTRA_ARCHS_tune-arm1176jzfs = "${PACKAGE_EXTRA_ARCHS_tune-armv6}"
PACKAGE_EXTRA_ARCHS_tune-arm1176jzfshf = "${PACKAGE_EXTRA_ARCHS_tune-armv6hf-vfp} arm1176jzfshf-vfp"

PREFERRED_PROVIDER_virtual/kernel = "linux-stable"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot"

KERNEL_DEVICETREE = "bcm2835-rpi-b.dtb"
KERNEL_DEFCONFIG = "bcm2835"

UBOOT_MACHINE = "rpi_b_config"
UBOOT_MAKE_TARGET = "u-boot.bin"
UBOOT_SUFFIX = "bin"
UBOOT_SCRIPT = "uEnv.txt"

SDIMG_BOOTFILES += " config.txt \
    bcm2835-bootfiles/bootcode.bin \
    bcm2835-bootfiles/start.elf \
    u-boot.bin \
  "

EXTRA_IMAGEDEPENDS += "bcm2835-bootfiles"

GUI_MACHINE_CLASS = "bigscreen"

SERIAL_CONSOLE = "115200 ttyAMA0"

MACHINE_FEATURES = "kernel26 apm usbhost keyboard vfat ext2 screen alsa wifi sdio"
MACHINE_FEATURES_BACKFILL_CONSIDERED = "rtc"
